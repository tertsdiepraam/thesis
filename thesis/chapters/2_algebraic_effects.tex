\chapter{Algebraic Effects}\label{chap:algebraic_effects}

The theory of algebraic effects makes working with effectful operations easier in two ways: it makes effects composable and separates the use and interpretation of effects, which provides a high degree of modularity. Crucially, however, it does not cover many higher-order operations; effect operations that take effectful computations as arguments. The theory of hefty algebras extends the theory of algebraic effects with elaborations for higher-order effects. Since Elaine is based on the theory of hefty algebras, the theory of algebraic effects also applies to Elaine. In this chapter, we give an introduction to algebraic effects. In the next chapter, we discuss its limitations regarding higher-order effects and describe how hefty algebras overcome those limitations.

\section{Monads}

We will build up the notion of algebraic effects from monads. Monads were introduced as an abstraction for effectful computation by \textcite{moggi_abstract_1989,moggi_computational_1989}. Shortly afterwards, monads were popularized as a technique for writing effectful programs in pure languages \autocite{wadler_essence_1992,peyton_jones_imperative_1993}.

Many definitions of monads can be given, and various analogies can be employed in explaining them. However, for our purposes, a monad is a type constructor with two associated functions: \hs{return} and \hs{>>=}, with the latter pronounced ``bind''. In Haskell, this concept is easily encoded in a type class.

\begin{lst}{Haskell}
class Monad m where
  return :: a -> m a
  (>>=)  :: m a -> (a -> m b) -> m b
\end{lst}
%
This type class dictates that \hs{return} constructs a value of type \hs{m a} for any monad \hs{m} and any type \hs{a}. This represents a computation that has no further effects and just ``returns'' a value. Additionally, we can compose two monadic computations using \hs{>>=}, which takes a monadic computation and a \emph{continuation}. The continuation is the function that should be called after the operation has been performed. The continuation is passed the return value of the operation. Intuitively, the \hs{>>=} operator therefore sequences two computations.

To explain how effectful operations can be encoded with this, we can look at a simple example: the \hs{Maybe} monad. Our goal with this monad is to create an ``abort'' effect, where the computation stops and returns immediately once the \hs{abort} operation is used, without evaluating the rest of the computation. While the returned value is wrapped in \hs{Just}, the computation continues, but once a computation returns \hs{Nothing}, the computation should abort. So, we define the \hs{abort} operation as \hs{Nothing}.

\begin{lst}{Haskell}
data Maybe a
  = Just a
  | Nothing

class Monad Maybe where
  return = Just
  
  Just a  >>= k = k a
  Nothing >>= k = Nothing

abort :: Maybe a
abort = Nothing
\end{lst}
%
With this definition, we can chain functions returning \hs{Maybe}. For example, we can define a \hs{head} function with the type \hs{[a] -> Maybe a} that returns the first element of a list if it is non-empty and \hs{Nothing} otherwise. We also define a division function which checks that the divisor is non-zero. These functions can then be composed using the \hs{>>=} operator, such that the composed computation returns \hs{Nothing} if one of these functions returns \hs{Nothing}.

\begin{lst}{Haskell}
head :: [a] -> Maybe a
head (x:xs) = Just x
head _ = Nothing

safeDiv :: Int -> Int -> Maybe Int
safeDiv _ 0 = Nothing
safeDiv x y = Just $ div x y

main = do
  print $ head []      >>= safeDiv 10 -- -> Nothing
  print $ head [0,1,2] >>= safeDiv 10 -- -> Nothing
  print $ head [2,3,4] >>= safeDiv 10 -- -> Just 5
\end{lst}
%
A more involved example is the \hs{State} monad. A function that modifies state needs to take the current state and return the new state. For example, a function that takes \hs{a}, returns \hs{b}, and modifies state \hs{s} needs to have the type \hs{a -> s -> (s, b)}.
The new state then needs to be passed to the next function that modifies state.

\begin{lst}{Haskell}
-- Increment the state by `a` and return the old state
inc :: Int -> Int -> (Int, Int)
inc a s = (s + a, s)

multipleIncs :: Int -> (Int, Int)
multipleIncs s = let
  (s' , _) = inc 5 s
  (s'', _) = inc 6 s'
  in inc 7 s''
\end{lst}
%
The types of all functions using state now end with the same pattern: \hs{s -> (s, b)}. This is an opportunity for abstraction, because we can define a type for that pattern. Since this type represents the state effect, this type is called \hs{State}.

\begin{lst}{Haskell}
newtype State s a = State (s -> (s, a))

-- so that the inc function becomes:
inc :: Int -> State Int Int
inc a = State (\s -> (s + a, s))
\end{lst}
%
This \hs{State s} type becomes a monad if we implement \hs{return} and \hs{>>=} for it. This allows us to compose functions returning the \hs{State} type. Additionally, we define the \hs{get} and \hs{put} functions as a basic interface for interacting with state.

\begin{lst}{Haskell}
instance Monad (State s) where
  return x = State (\s -> (s, x))

  State fa >>= k = State $ \s ->
    let (s', a) = fa s
        State fb = k a
     in fb s'

get :: State s s
get = State (\s -> (s, s))

put :: s -> State s ()
put s = State (\_ -> (s, ()))
\end{lst}
%
To retrieve the final value of the computation, we define a function \hs{runState}, which takes an initial state and returns a pair of the final state and the returned value.

\begin{lst}{Haskell}
runState :: s -> State s a -> (s, a)
runState initialState (State func) = func initialState
\end{lst}
%
The \hs{inc} operations can then be sequenced using the \hs{>>=} operator. Because the return value of \hs{inc} is irrelevant in the computation, we define a shorthand operator \hs{>>}, which ignores the return value of the first operation.

\begin{lst}{Haskell}
(>>) :: Monad m => m a -> m b -> m b
a >> b = a >>= \_ -> b

inc :: Int -> State Int Int
inc x = get >>= \s -> put (s + x) >>= return s

multipleIncs :: State Int Int
multipleIncs = inc 5 >> inc 6 >> inc 7

main = print (runState 0 bar) -- prints 0 + 5 + 6 + 7 = 18
\end{lst}
%
This is the power of monads: they allow us to abstract over the effectful operations, while also declaring the effects that a function requires in its return type. In the final example, we do not have to think about how the \hs{State} monad works any more, but only use the \hs{get} and \hs{put} operations to build complex computations. The abstraction separates the interface from the implementation. This modularity is one of the core motivations of the study of effects. 

To make working with monads more convenient, Haskell also features do-notation, which is syntactic sugar for the \hs{>>=} and \hs{>>} operators. Using do-notation, the \hs{multipleIncs} computation from the previous example can be written as:

\begin{lst}{Haskell}
multipleIncs = do
  inc 5
  inc 6
  inc 7
\end{lst}
%
If the results from the \hs{inc} computations needs to be used, the \hs{<-} operator, which is part of do-notation, can be used to bind the result of a computation to a variable. For example, the sum of all the results from the \hs{inc} calls can be returned.

\begin{lst}{Haskell}
multipleIncs' = do
  a <- inc 5
  b <- inc 6
  c <- inc 7
  return (a + b + c)

-- which is equivalent to
multipleIncs' = 
  inc 5 >>= \a ->
    inc 6 >>= \b ->
      inc 7 >>= \c ->
        return (a + b + c)
\end{lst}
%
Monads are a convenient model for programming with effects in Haskell, while also staying true to its functional paradigm. However, monads are also limited, since they cannot be composed. Imagine, for instance, a computation that decrements some state and returns the new value, but also asserts that the value never becomes negative and returns \hs{Nothing} in that case. This computation might look as follows.

\begin{lst}{Haskell}
decrement :: State Int (Maybe Int)
decrement = get >>= \s ->
              if s > 0
              then put (s - 1) >> return (return (s - 1))
              else return abort
\end{lst}
%
The abort effect has to be wrapped in additional \hs{return} calls and does not interact with the state. The type \hs{State Int (Maybe Int)} is not a combined monad for both effects, but one monad wrapped in another. Instead, we need some combined monad \hs{MaybeState} that combines the operations of both monads.

\begin{lst}{Haskell}
decrement :: MaybeState Int Int
decrement = get >>= \s ->
              if s > 0
              then put (s - 1) >> return (s - 1)
              else abort
\end{lst}
%
While it is possible to define such a monad, we would need to define one for every combination of monad operations that we would like to use, which quickly becomes cumbersome. Hence, we need to look elsewhere for a solution. One solution to this is to use monad transformers, as explained in \cref{sec:monad_transformers}. Another solution is to compose effects in the \emph{free monad}.

\section{Composing Effects with the Free Monad}

The free monad is a monad that encodes the structure of a program without imposing semantics \autocite{swierstra_data_2008}. The free monad takes a functor \hs{f} as an argument. The free monad then gives a syntactic description of the operations given by that functor. It is therefore the trivial monad parametrized by the operations of \hs{f}. In Haskell, the free monad is implemented as the \hs{Free} data type. The definitions in this section are based on the implementation by \textcite{kammar_handlers_2013} and the encoding of the free monad by \textcite{bach_poulsen_hefty_2023}.

\begin{lst}{Haskell}
data Free f a
  = Pure a
  | Op (f (Free f a))

instance Functor f => Monad (Free f) where
  return = Pure
  Pure x >>= f = f x
  Op g >>= f = Op (fmap (>>= f) g)
\end{lst}
%
This type class states that, if \hs{f} is some functor, then \hs{Free f} is monad. For example, if we make some functor \hs{State s}, then \hs{Free (State s)} is a monad. Of course, this is only useful if \hs{Free (State s)} is a monad with the same functionality as the original state monad. These functors are defined with a constructor for each effect operation. Since the free monad encodes the syntax of the effect operations, we define the \hs{State} functor with constructors corresponding to the operations of the state effect: \hs{get} and \hs{put}. This is a functor over the \hs{k} parameter, which represents the \emph{continuation}. Note that we do not have to give definitions of \hs{return} and \hs{>>=} since those are defined generically for any \hs{f} on \hs{Free}. We only have to derive the default \hs{Functor} instance.

\begin{lst}{Haskell}
data State s k = Put s k | Get (s -> k)
  deriving Functor
\end{lst}
%
Similarly, we can write the abort effect as a free monad, which we previously implemented using the \hs{Maybe} monad. Recall that there is only one operation for this effect: \hs{abort}. Hence, the \hs{Abort} functor only needs a single constructor, which we also call \hs{Abort}. The \hs{Abort} constructor does not use the continuation because it signals that evaluation should stop.

\begin{lst}{Haskell}
data Abort k = Abort
  deriving Functor
\end{lst}
%
In contrast with monads, these functors can be meaningfully composed. We define a type-level operator \hs{+}, which represents a coproduct for functors. This operator can be thought of as \hs{Either} for functors, since \hs{Either} is the coproduct for types. We use this operator to build lists of functors. Just like lists have a \hs{Cons} and \hs{Nil}, these lists consist of \hs{+} and \hs{End}, where \hs{End} is a functor without any constructors.

\begin{lst}{Haskell}
infixr 6 +
data (f + g) a = L (f a) | R (g a)
  deriving Functor

data End k
  deriving Functor
\end{lst}
%
The \hs{End} functor has the property that it does not add any operations. Therefore, we have that \code{Free (f + End)} is functionally the same as \code{Free f} and that \code{Free End a} is equivalent to \hs{a}. We can then make monads for any combination of the functors we have defined, such as \hs{Free (State s + End)}, \hs{Free (Abort + End)}, or \hs{Free (State s + Abort + End)}. In general, we can construct a monad for any set of functors.

However, we have no way to use any of the effect operations. For example, if we have the monad \hs{Free (State s + Abort + End)}, how would we use the \hs{get} operation that we expect from the state monad? The solution is to give a definition for \hs{get} for the free monad if and only if \hs{State} is one of the composed functors. We do this with a type class relation \hs{<}, which defines an injection from a functor \hs{f} to any composed functor \hs{g} that contains \hs{f}. We can use this injection to define the \hs{get}, \hs{put}, and \hs{abort} functions. These functions are called \emph{smart constructors}.

\begin{lst}{Haskell}
class f < g where
  inj :: f k -> g k

instance f < f where inj = id
instance f < (f + g) where inj = L
instance f < h => f < (g + h) where inj = R . inj

get :: State s < f => Free f s
get = Op $ inj $ Get Pure

put  :: State s < f => s -> Free f ()
put s = Op $ inj $ Put $ Pure ()

abort :: Abort < f => Free f ()
abort = Op $ inj $ Abort
\end{lst}
%
This makes it possible to construct a computation using all those operations. For example, a computation that checks the state, asserts that it is larger than 0, and then decrements the state by 1.

\begin{lst}{Haskell}
decrement :: Free (State Int + Abort + End) Int
decrement = get >>= \s ->
              if s > 0
              then put (s - 1) >>= return (s - 1)
              else abort 
\end{lst}
%
To evaluate this computation, we need to define an algebra for the \hs{Free} data type. Conceptually, the evaluation should remove the effects one by one from the monad. To do that, there needs to be a function with the type
\[\mcode{Free (f + f') a -> Free f' b}\]
for every \hs{f} and finally a function \hs{Free End a -> a} to reduce the free monad to a final value. Following \textcite{castagna_handlers_2009}, these functions are called \emph{handlers}. In general, any handler operating on \hs{Free} needs two take two cases into account, since \hs{Free} has two constructors: \hs{Pure} and \hs{Op}. By using a fold over \hs{Free} we can define a handler in terms of two smaller functions, corresponding to these two constructors:
\begin{lst}{Haskell}
fold :: Functor f => (a -> b) -> (f b -> b) -> Free f a -> b
fold gen _   (Pure x) = gen x
fold gen alg (Op f)   = alg (fmap (fold gen alg) f)

-- the return case
ret :: a -> Free f' b
-- and the case for handling operations
op  :: f (Free f' b) -> Free f' b
\end{lst}
% \begin{itemize}
%     \item the return case, \hs{a -> Free f' b};
%     \item and the case for handling the operations: \hs{f (Free f' b) -> Free f' b}.
% \end{itemize}
However, to generalize the handler, we add a parameter \hs{p} as well, This parameter can be used by the handlers can thread some value through the computation. For some effects, this parameter will simply be the unit value \hs{()}, but for the state effect, this parameter will represent the state. With this parameter, the \hs{handle} function, which constructs handlers, is defined as follows:

\begin{lst}{Haskell}
handle :: (Functor f, Functor f')
          -- a function for the return case:
       => (a -> p -> Free f' b)
          -- a function for handling operations:
       -> (f (p -> Free f' b) -> p -> Free f' b)
          -- the type of the resulting handler: 
       -> Free (f + f') a -> p -> Free f' b
handle ret f = fold ret $
  \case
    L x -> f x
    R x -> \p -> Op $ fmap (\m -> m p) x
\end{lst}
%
Handlers for the various effects can then be constructed using \hs{handle}. For each computation, we need a handler for each effect to remove them from the free monad so that only the \hs{End} functor remains.

\begin{lst}{Haskell}
handleAbort :: Functor f => Free (Abort + f) a -> Free f (Maybe a)
handleAbort c = handle
  (\a _ -> return $ Just a)
  (\Abort () -> return Nothing) 
  c ()
  
handleState :: Functor f => s -> Free (State s + f) a -> Free f (s, a)
handleState = flip $ handle
  (\x s -> return (s, x))
  (\x s -> case x of
      Put s' k -> k s'
      Get k -> k s s)
\end{lst}
%
%Then we can reduce \hs{Free End a} to \hs{a}.
Applying the handlers for all effects leaves us with \hs{Free End a}, which should be reduced to \hs{a}. This is done with the \hs{handleEnd} function.

\begin{lst}{Haskell}
-- The Op case does not need to be handled since End
-- cannot be constructed
handleEnd :: Free End a -> a
handleEnd (Pure a) = a
\end{lst}
%
Finally, we can reduce a free monad with multiple effects to a single result by applying all handlers for the effects. So, the \hs{decrement} function above requires handlers for \hs{State s}, \hs{Abort}, and \hs{End} and can be evaluated as follows:

\begin{lst}{Haskell}
result :: (Int, Maybe Int)
result = handleEnd $ handleState (0::Int) $ handleAbort decrement  
\end{lst}
%
With this encoding, we can use the abort and state effects together, while providing separate handlers per effect. Note that the order in which the handlers are applied matters for the return type of \hs{result}. If abort is handled first and state second, the final type is \hs{(Int, Maybe Int)}. If state is handled first, it is \hs{Maybe (Int, Int)}.

While the plumbing needed for a free monad is extensive, it has many advantages over regular monads. First, we can combine multiple functors in our type signatures. Second, we can define operations that work for any effect composition that contains an effect. Third, we can provide modular handlers that handle a single effect from the composed functors. If all effects are defined in this way, then effect is automatically compatible with all other effects.

Finally, we have not only gained modularity for the effects themselves, but also for the handlers. The effects have become an interface, while the handlers provide the semantics. Within this framework, the semantics of effects can be changed without touching the type and definition of the computation. There is nothing preventing different implementations of the handlers. It is, for example, possible to define a state handler in which \hs{put} operations are ignored, keeping the state is constant.

\section{Algebraic Effects}

The free monad encoding in the previous section is an implementation of algebraic effects in Haskell. The term ``algebraic'' comes from the fact that this method works for effects that can be described as algebraic theories \autocite{goos_adequacy_2001}. Later, \textcite{plotkin_algebraic_2003} showed that this is only possible for effects that satisfy the \emph{algebraicity property}.

The algebraicity property states that the \hs{>>=} operation distributes over the computation parameters of an operation. This means that if there is some operation \hs{op} that has some parameter of type \hs{k} then the following computations should be equivalent for some continuation \hs{k'}:
\[ \mcode{(op k) >>= k'}\quad==\quad\mcode{op (k >>= k')} \]
So, if the state effect satisfies the algebraicity property, the following equality should hold for any continuations \hs{k} and \hs{k'}:
\[ \mcode{(Op (Put s k)) >>= k'}\quad==\quad\mcode{Op (Put s (k >>= k'))} \]
Intuitively, this matches how we expect state to behave: if the state is changed, it will remain changed throughout the rest of the continuation, until it is changed again.

By construction, the algebraicity property holds for any effect we have defined in the previous section. This can be derived from the definitions of \hs{>>=} on \hs{Free} and \hs{fmap} for the effects. Indeed, we can apply the definitions to \hs{Free (State s)} to verify that the algebraicity property holds.

\begin{lst}{Haskell}
Op (Put s k) >>= k'
-- apply >>= of Free:
Op (fmap (>>= k) (Put s k'))
-- apply fmap of Put:
Op (Put s (k >>= k'))
\end{lst}
%
Consequently, any effect that does not satisfy the algebraicity property cannot be written as the free monad. While the state and abort effects satisfy this property, \emph{higher-order effects}, do not. Examples of higher-order effects are exception catching and the reader effect with a local operation. Those effects are discussed in \cref{chap:higher_order}.

\section{Building a Language with Algebraic Effects}

Although the previous sections contain an encoding of algebraic effects, there are details in this encoding that we might like to hide. For instance, writing all return types as \hs{Free f a} for every function becomes repetitive. Every returned value also needs to be wrapped in \hs{pure} to be mapped to the monad. Our goal is then to remove as much of the additional syntax that is required to work with effects when compared to pure functions.

This is where we reach the limits of what we can achieve with the encoding of the free monad in Haskell. If we instead design a new language which integrates algebraic effects as a core feature of the language, we have much more freedom in designing a syntax and semantics that work well for this purpose.

Elaine is a language with support for algebraic effects, but it also supports higher-order effects. Therefore, this section focuses on Koka \autocite{leijen_koka_2014,leijen_koka_2023}, which only supports algebraic effects. Since Elaine is heavily inspired by Koka, the same concepts apply to Elaine.

At the core of such languages lies the following concept: all functions implicitly return the free monad with some effects. Therefore, we write \code{a -> e b}, which is analogous to \code{a -> Free e b} in the Haskell encoding. In that signature, we call \code{e} the \emph{effect row} and its elements as \emph{effects}. So, the function signature \hs{a -> e b} should be read as: ``this function takes an \hs{a} and returns \hs{b} with effect row \hs{e}.''

Instead of using type-level operators, we can introduce special syntax for effect rows, too. Following Koka, we will write effect rows as
\begin{center}\texttt{<e1,e2,...,eN>}.\end{center}
Additionally, effects becomes a special construct separate from monads and functors. Therefore, effect rows can get special treatment in the type system. It should be able to, for example, reason about equality between effect rows with the same effects in different orders, such as \el{<a,b>} and \el{<b,a>}. In the type system, we are then allowed to use different orders of effects interchangeably. We can reason about inclusion of effects in an effect row with the following notation:
\begin{center}\texttt{<e1,e2,...,eN|es>,}\end{center}
where \el{es} is the tail of the effect row; a variable representing the effect row with which this effect row can be extended. For example, the effect row \el{<abort|e>} consists of the \el{abort} effect and some other effect row \el{e}. 
x
We can define the same effects as before, such as the state and abort effects, but in Koka, we do not define them as functors. Instead, we define them using the \code{effect} keyword and each constructor of the functors is then declared with the \code{ctl} keyword.

\begin{lst}{Koka}
effect abort
  ctl abort(): a

effect state<a>
  ctl get(): a
  ctl put(x: a): ()
\end{lst}
%
The equivalent of \hs{Free (State s + Abort + End) a} is then \el{<state<s>,abort> a}. The equivalent of a handler would then be a function which takes \el{() -> <f|e> a} and returns \el{<|e> a}. In Koka, such a function can be defined with the \el{handler} construct, which requires an implementation for each operation of an effect and a special function for the return case.

Note the similarity to the \hs{handle} function we defined in Haskell before. In the case of abort effect, this handler is assigned to variable for later use. The state handler resembles the original state monad: it takes a computation \code{() -> <state<s>|e> a} and returns \hs{s -> e (s, a)}. For example, the \code{return} arm yields the anonymous function \code{fn(x) (s, x)}. The continuation can be called in a handler with the \code{resume} function in Koka. Since handling is defined by a fold, just like in the Haskell encoding, the effect is already handled in the continuation. For the state effect, \code{resume} therefore returns a function with the type \code{s -> e (s, a)}.

\begin{lst}{Koka}
val hAbort = handler
  return(x)   Just(x)
  ctl abort() Nothing

fun hState(init, c)
  fun h(c')
    with handler
      return(x)  fn(s) (s, x)
      ctl get()  fn(s) resume(s)(s)
      ctl put(n) fn(_) resume(())(n)
    c'()
  
  h(c)(init)
\end{lst}
%
In the free monad encoding in Haskell, the state had to be passed through the handlers as a parameter. Koka is a bit more flexible and allows us to return values with effectful computations. Therefore, it does not need the additional parameter.

Koka helps us hide some details, but the structure in the listing above is largely the same as with the free monad encoding. The larger differences become apparent when we want to use the effects in some computation. A port of the decrement function is listed below.

\begin{lst}{Koka}
fun decrement(): <state<int>,abort> int
  val s = get()
  if s == 0 then
    abort()
  
  put(s - 1)
  s - 1 

val x = hAbort { hState(3, decrement) } // -> Just(2)
val y = hAbort { hState(0, decrement) } // -> Nothing
\end{lst}
%
The \hs{>>=} operator is entirely implicit here. Therefore, it is similar to Haskell's do-notation. However, in do-notation, every effectful operation needs to be on a separate line. For example, if the state needs to be incremented by 1, this can be achieved in one line in Koka, but in Haskell using do-notation requires two lines.

\begin{lst}{Koka}
put(get() + 1)
\end{lst}
\begin{lst}{Haskell}
do
  x <- get
  put (x + 1)
\end{lst}
%
In Koka, effectful operations can be used anywhere as long as they are wrapped in a corresponding handler. In the end, the syntax is closer to imperative programming languages than functional programming languages. However, the type system still very much resembles that of a functional language. This is important because this means that we have not lost any of the type safety that the monadic treatment of effects provides. The signature of a function in Koka still gives a complete specification of all effects that a function might perform. In imperative languages, this information is entirely missing from the function signature. For example, the type system can assert that a function is entirely pure. In the listing below, the \hs{<>} in the type of the function asserts that it does not require effects, yet the \hs{println} function requires an effect. Hence, Koka's type checker will yield a type error.

\begin{lst}{Koka}
fun should_be_pure(x: int): <> int
  println("This will give a type error!")
  x + 10
\end{lst}
%
As will become clear in \cref{chap:basics}, Elaine takes a lot of inspiration from Koka. Handlers and effects are defined in the same way, modulo some syntactic differences. What sets Elaine apart, is that it also supports higher-order effects, which will be explained in the next chapter.
