\chapter{Introduction}\label{chap:introduction}

In many programming languages, computations are allowed to have \emph{effects}. This means that they can perform operations besides producing output, and interact with their environment. A computation might, for instance, read or modify a global variable, write to a file, throw an exception, or even exit the program.

Historically, programming languages have supported effects in different ways. Some programming languages opt to give the programmer virtually unrestricted access to effectful operations. For instance, any part of a C program can interact with memory, the filesystem or the network. The program can even yield control to any location in program with the \code{goto} keyword, which has famously been criticized by \textcite{dijkstra_letters_1968}, who argues that \code{goto} breaks the structure of the code. The programmer then has to trace the execution of the program in their mind in order to understand it. The same reasoning extends to other effects: the more effects a function is allowed to exhibit, the harder it becomes to reason about.

The ``anything goes'' approach to effects therefore puts a large burden of ensuring correct behaviour of a program on the programmer. If the language cannot provide any guarantees about what (a part of) a program can do, the programmer has to check instead. For instance, if a function somewhere in the code sets global variable to some incorrect value. This can then cause seemingly unrelated parts of the program to behave incorrectly. The programmer tasked with debugging this issue then has to examine the program as a whole to find where this modification takes place. In languages where this is possible, effectful operations therefore limit our ability to split the code into chunks to be examined separately.

A solution is to treat effects in a more structured manner. For example, instead of allowing \code{goto}, a language might provide exceptions. In a language like Java, checked exceptions are part of the type system, so that the type checker can verify that all exceptions are handled. However, with this approach, any effect must be backed by the language. That is, the language needs to have a dedicated feature for every effect that should be supported in this way and new effects cannot be created without adding a new feature to the language. This means that the support for various effects is limited to what the language designers have decided to add.

In contrast, languages adhering to the functional programming paradigm disallow effectful operations altogether.\footnote{Usually there are some escape hatches to this rule, such as Haskell's \hs{trace} function, which is built-in and effectful, but only supposed to be used for debugging.} Here, all functions are \emph{pure}, meaning that they are functions in the mathematical sense: only a mapping from inputs to output. Such a function is \emph{referentially transparent}, meaning that it always returns identical outputs for identical inputs and does not interact with the environment. By requiring that all functions are pure, a type signature of a function becomes almost a full specification of what the function can do.

However, sometimes effectful operations are still desired. Consider the following program written in Koka, a functional language where function need to be pure. In this program, there is a set of users that are considered administrators. The \code{all_admins} function checks whether all user ID's in a list are administrators.

\begin{lst}{Koka}
val admins = [0,1,2]

fun is_admin(user_id: int): bool
  admins.any(fn(x) x == user_id)

fun all_admins(l: list<int>): bool
  l.map(is_admin).foldl(True, (&&))

val result = all_admins([0,1,2,3])    
\end{lst}
%
This a fairly standard functional program where the result is a single boolean. However, the program does not tell us which users are not admins, which could be useful information to print. In an imperative language, we could just add a \code{print} call in \code{is_admin} to log any user that was not an admin and call it a day. But in a functional language, we cannot do this. Instead, each message we want to log needs to be returned \code{is_admin}. These messages then need to be concatenated to build up the string that should be printed.

\begin{lst}{Koka}
fun is_admin(user_id: int): (bool, string)
  if admins.any(fn(x) x == user_id)
  then (True, "")
  else (False, "Denied " ++ show(user_id) ++ "\n")

fun all_admins(list: list<int>): (bool, string)
  match list
    Nil() -> (True, "")
    Cons(x, xs) ->
      val (y, s) = is_admin(x)
      val (ys, s') = all_admins(xs)
      (y && ys, s ++ s')

val (result, log) = all_admins([1,2,3,4])
\end{lst}
%
So, adding some logging made the program much more complicated. For larger programs, one might imagine that programming with effects in a functional language therefore quickly becomes laborious. Additionally, the functions above are adapted specifically to our logging effect; using any other effect would require a different implementation. Therefore, we should abstract over the effects in the computation.

This abstraction can be found in the form of \emph{monads} \autocite{wadler_essence_1992,peyton_jones_imperative_1993}. A monad represents a computation with some effect. It is a type constructor that takes the return type of the computation as a parameter. For a type to be a monad, it needs to define two functions: \hs{return} and \hs{>>=}. The former wraps a value in the monad and the latter sequences 2 monadic computations. In Koka, we cannot call these functions \hs{return} and \hs{>>=}, so we call them \code{pure} and \code{bind}, respectively.

\begin{lst}{Koka}
alias log<a> = (v: a, msg: string)

fun pure(v: a): log<a>
  (v, "")

fun bind(m: log<a>, k: a -> log<b>): log<b>
  val (a, s) = m
  val (b, s') = k(a)
  (b, s ++ s')

fun log(msg: string): log<()>
  ((), msg)
\end{lst}
%
The \code{is_admin} and \code{all_admins} can then be written using these functions instead of dealing with the strings in the tuples directly. Hence, we have abstracted over the effect and could replace it with another. Specifically, we could change the effect that \code{is_admin} uses without changing \code{all_admins}.

\begin{lst}{Koka}
fun all_admins(list)
  match list
    Nil() -> pure(True)
    Cons(x, xs) -> is_admin(x).bind fn(y)
      all_admins(xs).bind fn(ys)
        pure(y && ys)
  
fun is_admin(user_id: int): log<bool>
  if admins.any(fn(x) x == user_id)
  then pure(True)
  else
    log("Denied " ++ show(user_id) ++ "\n").bind fn(())
      pure(False)
\end{lst}
%
In fairness, Koka is not built for monadic operations and other languages provide more convenient syntax for monads. However, the structure of the same program in such a language would be roughly the same.

Another limitation of the monad approach becomes apparent when we want to use multiple effects. The problem is that the composition of two monads does not yield a monad. This is a limitation that can be worked around with \emph{monad transformers}. A monad transformer takes a monad and adds operations to it. The operations of every effect then need to be implemented on every transformer. Adding a single effect therefore requires additional implementations of its operations every other monad transformer. The number of implementations therefore grows quadratically with the number of effects.

To overcome these limitations, we instead turn to the theory of \emph{algebraic effects}, which allow effects to be defined modularly. In this theory, an effect consists of a set of \emph{effect operations}. A computation using an effect then needs to be wrapped in a \emph{handler}, which defines the semantics for the operations. These modular effects and handlers are based on the free monad. It is possible to encode the free monad in Haskell and use algebraic effects in Haskell that way.

However, Koka supports algebraic effects as a first-class construct. This allows Koka to make the use of algebraic effects very easy. It is not the only language to do this; other examples include Frank \autocite{lindley_be_2017}, Effekt \autocite{bach_poulsen_hefty_2023}, Eff \autocite{bauer_programming_2015}, Helium \autocite{biernacki_abstracting_2019}, and OCaml \autocite{sivaramakrishnan_retrofitting_2021}. In the listing below, we first declare the algebraic effect \code{log}. This effect has a single operation also called \code{log}, which takes the message to log as an argument. Then we define a handler \code{hLog} for the \code{log} effect. The handler transforms the effectful computation into a monad, which matches our tuple from before. Note that the \code{return} branch matches the \code{pure} function and that the \code{log} branch combines the \code{bind} and \code{log} functions in our monad implementation. The \code{is_admin} and \code{all_admin} functions then simply declare that they use the \code{log} effect, which allows them to use the \code{log} operation. This relies on the fact that Koka's \code{map} and \code{foldl} functions are generic over effects in the computation.

\begin{lst}{Koka}
effect log
  ctl log(msg: string): ()
  
val hLog = handler
  return(x) (x, "")
  ctl log(msg)
    val (x, msg') = resume(())
    (x, msg ++ msg')
  
fun is_admin(user_id: int): <log> bool
  val result = admins.any(fn(x) x == user_id)
  if !result then
    log("Denied " ++ show(user_id) ++ "\n")
  result
 
fun all_admins(l): <log> bool
  l.map(is_admin).foldl(True, (&&))
  
val (result, log) = hLog { [1,2,3,4].all(is_admin) }
\end{lst}
%
In the end, the implementation then looks very much like imperative code, but the type system still resembles the type system of functional languages. Effects are handled in a structured way, but are still convenient to use. There are several other advantages too. The effects are modular and can be combined easily. Additionally, the handlers are modular; any handler can be swapped out for another handler, changing the semantics of the effect. For example, we could write a handler that ignores all \code{log} calls or stores the logged messages in a list.

However, some effects are not algebraic and can therefore not be represented as effects in a language like Koka. \emph{Higher-order effects} are effects with operations that take effectful computations as arguments, and they are not algebraic in general. As \textcite{castagna_handlers_2009} have shown, they can be written as handlers, but not as effect operations. This is known as the \emph{modularity problem} for higher-order effects \autocite{wu_effect_2014}. Several extensions to algebraic effects have been proposed to accommodate for higher-order effects \autocite{wu_effect_2014,oh_latent_2021}. One such extension is \emph{hefty algebras} by \textcite{bach_poulsen_hefty_2023}, which introduces elaborations to implement higher-order effects. Elaborations give semantics to higher-order effects by translating them into computations with only algebraic effects. This means that evaluation of a computation becomes a two-step process: first higher-order effects are elaborated into algebraic effects, which can then handled. Like handlers, elaborations are modular, and it is possible to define multiple elaborations for a single effect.

Therefore, there currently exist languages with algebraic effects and there is a theory for hefty algebras, but there is no language yet based on hefty algebras. This is the gap in the research that this thesis aims to fill. The question we therefore wish to answer is:
\begin{center}
\textbf{How can we design a language with higher-order effects and elaborations with hefty algebras as underlying theory?}
\end{center}
In this thesis, we introduce a novel programming language called \emph{Elaine}. The core idea of Elaine is to define a language which features elaborations and higher-order effects as first-class constructs. This brings the theory of hefty algebras into practice. With Elaine, we aim to demonstrate the usefulness of elaborations as a language feature. Throughout this thesis, we present example programs with higher-order effects to argue that elaborations are an intuitive construct for higher-order effects.

Like handlers for algebraic effects, elaborations require the programmer to specify which elaboration should be applied. However, elaborations have several properties which make it likely that there is only one relevant possible elaboration. Hence, we argue that elaboration instead should often be implicit and inferred by the language. To this end, we introduce \emph{implicit elaboration resolution}, a novel feature that infers an elaboration from the variables in scope.

Additionally, we give transformations from higher-order effects to algebraic effects. There are two reasons for defining such a transformation. The first is to show how elaborations can be compiled in a larger compilation pipeline. The second is that these transformations show how elaborations could be added to existing systems for algebraic effects.

We present a specification for Elaine, including the syntax definition, typing judgments, and semantics. Along with this specification, we provide a reference implementation written in Haskell in the artifact accompanying this thesis. This implementation includes a parser, type checker, interpreter, pretty printer, and the transformations mentioned above. Elaine opens up exploration for programming languages with higher-order effects.

\paragraph{Contributions} The main contribution of this thesis is the specification and implementation of Elaine. This consists of several parts.
%
\begin{itemize}
    \item We present a language with both handlers and elaborations based on hefty algebras (\cref{chap:basics}). The language specification consists of a syntax definition, typing judgments and reduction semantics. (\cref{chap:spec}). We conjecture that hefty algebras are the semantics of the language, but we do not prove this.
    %
    \item We generalize effect rows to higher-order effects (\cref{sec:hoeffectrow}). The type system of the language is based on these generalized effect row.
    %
    \item We provide a prototype for the language with a parser, a type checker based on Hindley-Milner type inference and an interpreter for this language implemented in Haskell. This prototype is available in the artifact.
    %
    \item We provide a set of examples for programming with higher-order effect operations in Elaine (\cref{chap:examples}). These examples provide evidence that elaborations are a suitable and convenient construct for writing programs with higher-order effects. These examples can be executed and produce the expected results with the prototype. The examples include the reader effect, the writer effect, structured logging and parser combinators, all of which are implemented using higher-order effects.
    %
    \item We provide a type-directed procedure for inferring elaborations, which alleviates the programmer from specifying which elaboration should be applied when it can be inferred from the context (\cref{chap:elabres}).
\end{itemize}
%
This thesis consists of the following parts. First, we give an overview of the relevant theory of algebraic effects in \cref{chap:algebraic_effects} and higher-order effects and hefty algebras in \cref{chap:higher_order}. Then, we present Elaine in \cref{chap:basics}. The implicit elaboration resolution is then discussed in \cref{chap:elabres}. Finally, we discuss related work in \cref{chap:related_work} and conclude in \cref{chap:conclusion}. The appendices contain additional examples of Elaine programs (\cref{chap:examples}) and a full specification of the language (\cref{chap:spec}).

\paragraph{Artifact}
The artifact accompanying this thesis contains a full prototype implementation for Elaine, written in Haskell. The \code{README.md} file contains instructions for building and executing the interpreter.

The source code of the parser, type checker, interpreter, and other aspects of the implementation can be found in the \code{src/Elaine} directory. The \code{examples} directory contains various example programs written in Elaine, including implementations of the reader effect, writer effect, exception effect, structured logging, and a set of parser combinators.

The artifact is available online at \url{https://github.com/tertsdiepraam/elaine}.
