\chapter{Elaboration Compilation}\label{chap:elabcomp}

Since Elaine has a novel semantics for elaborations, it is worth examining its relation to well-studied constructs from programming language theory. Therefore, we introduce a transformation from programs with higher-order effects to a program with only algebraic effects, translating higher-order effects into algebraic effects, while preserving their semantics.

The goal of this transformation is twofold. First, it further connects hefty algebras and Elaine to existing literature. For example, by compiling to a representation with only algebraic effects, we can then further compile the program using existing techniques, such as the compilation procedures defined for Koka \autocite{leijen_type_2017}. In this thesis and the accompanying implementation, we provide the first step of this compilation. Second, the transformation allows us to encode elaborations in existing libraries and languages for algebraic effects.

\section{Non-locality of Elaborations}

\TODO{Actually it's not just non-locality but also undecidable}

Examining the semantics of elaborations, we observe that elaborations are similar to macros: they perform a syntactic substitution. For instance, the program on the left transforms into the program on the right by replacing \el{plus_two!}, with the expression \el{\{ x + 2 \}}.

\begin{minipage}[b]{0.5\textwidth}
\begin{lstlisting}[language=elaine,style=fancy]
use std;

effect PlusTwo! {
    plus_two!(Int)
}

let ePlusTwo = {
    elaboration PlusTwo! -> <> {
        plus_two!(x) { add(x, 2) }
    }
};

let main = elab plus_two!(5);
\end{lstlisting}
\end{minipage}
\begin{minipage}[b]{0.5\textwidth}
\begin{lstlisting}[language=elaine,style=fancy]
let main = { add(5, 2) };
\end{lstlisting}
\end{minipage}

Additionally, the location of the \el{elab} does not matter as long as the operations are evaluated within it. For instance, these expressions are equivalent:

\begin{minipage}[b]{0.5\textwidth}
\begin{lstlisting}[language=elaine,style=fancy]
let main = elab[e] {
    a!();
    a!()
};
\end{lstlisting}
\end{minipage}
\begin{minipage}[b]{0.5\textwidth}
\begin{lstlisting}[language=elaine,style=fancy]
let main = {
    elab[e] a!();
    elab[e] a!()
};
\end{lstlisting}
\end{minipage}

In some cases, it is therefore possible to statically determine the elaboration that should be applied. In that situation, we can remove the elaboration from the program by performing the syntactic substitution.

However, we cannot apply that technique in general. One example where it does not work is when the elaboration is given by a complex expression, such as an \el{if}-expression:
\begin{lstlisting}[language=elaine,style=fancy]
elab[if cond { elab1 } else { elab2 }] c
\end{lstlisting}

Moreover, a single operation might need to be elaborated by different \el{elab} constructs, depending on run-time computations. In the listing below, there are two elaborations \el{eOne} and \el{eTwo} of an operation \el{a!()}. The \el{a!()} operation in \el{f} is elaborated where \el{f} is called. If the condition \el{k} evaluates to \el{true}, \el{f} is assigned to \el{g}, which is elaborated by \el{eOne}. However, if \el{k} evaluates to \el{false}, \el{f} is called in the inner \el{elab} and hence \el{a!()} is elaborated by \el{eTwo}.

\begin{lstlisting}[language=elaine,style=fancy]
elab[eOne] {
    let g = elab[eTwo] {
        let f = fn() { a!() };
        if k {
            f
        } else {
            f();
            fn() { () }
        }
    }
    g()
}
\end{lstlisting}

Therefore, the analysis of determining the elaboration that should be applied to an operation is non-local. The static substitution could be used as an optimization or simplification step, but it cannot guarantee that the transformed program will not contain higher-order effects.

\section{Operations as Functions}

As explained in \cref{sec:semantics}, higher-order operations are evaluated differently from functions. The main difference is that the arguments are thunked and passed by name, instead of by value. 

This behaviour can be emulated for functions if anonymous functions are passed as arguments instead of expressions. That is, for any operation call \el{op!(e1, ..., eN)}, we wrap the arguments into functions to get \el{op(fn() \{ e1 \}, ..., fn() \{ eN \})}. In the body of \el{op}, we then replace each occurrence of an argument \el{x} with \el{x()} such that the thunked value is obtained. The \el{op} operation can then be evaluated like a function instead, but it still has the intended semantics.

\section{Compiling Elaborations to Dictionary Passing}

Instead, the elaborations can be transforms with a technique similar to dictionary-passing style: the implicit context of elaborations is explicitly passed to functions that require a higher-order effect.

Any function with higher-order effects then takes the elaboration to apply as an argument and the operation is wrapped in an \el{elab}. The elaboration is then taken from \el{elabA} at the call-site.

\begin{lstlisting}[language=elaine,style=fancy,float=p,caption={\fixme{Some explanation. Should be higher-order?}}]
use std;

effect A! {
    arithmetic!(Int, Int) Int
}

let eAdd = elaboration A! -> <> {
    arithmetic!(a, b) { add(a, b) }
};

let eMul = elaboration A! -> <> {
    arithmetic!(a, b) { mul(a, b) }
};

# The original program
let foo = fn(k) {
    elab[eAdd] {
        let g = elab[eMul] {
            let f = fn() { a!(5, 2 + 3) };
            if k {
                f
            } else {
                let x = f();
                fn() { x }
            }
        };
        g()
    }
};

# Create a new type with one constructor to represent the
# elaboration. The fields of the constructor are the 
# operations. We assume for convenience that all the
# generated identifiers do not conflict with existing
# identifiers.
type ElabA {
    ElabA(fn(fn() Int, fn() Int) Int)
}

# Convenience function to access the operation a from A!
let elab_A_a = fn(e: ElabA) {
    let ElabA(v) = e;
    v
};

let eAdd = ElabA ( fn(a, b) { add(a(), b()) } );
let eMul = ElabA ( fn(a, b) { mul(a(), b()) } );

# The transformed program
let foo' = fn(k) {
    let elab_A = eAdd;
    let g = {
        let elab_A = eMul;
        let f = fn(elab_A) { elab_A_a(elab_a)(fn() { 5 }, fn() { 2 + 3) } };
        if k {
            f
        } else {
            let x = f(elab_A);
            fn(elab_A) { x }
        }
    };
    g(elab_A_a)
};
\end{lstlisting}

\section{Handlers as Dictionary Passing}

We now have two pieces of the puzzle: we know how to translate the semantics for higher-order operations into regular functions, and we have determined that we need something like dictionary passing to determine what elaboration should be applied. The problem is that our language -- as currently defined -- does not support dictionaries. Even if dictionaries were defined, the type safety of the transformed program would be difficult to check without a sub-structural type system.

This leads to the final observation: effect handlers can be used as our dictionary passing construct. Conceptually, both \el{elab} and \el{handle} work similarly: they define a scope in which a given elaboration or handler is used. This scope is the same for both. We start by defining a handler that returns an elaboration for higher-order effect \el{A!}, much like the \el{Ask} effect from \cref{chap:basics}.

\begin{lstlisting}[language=elaine,style=fancy]
let hElab = fn(e) {
    handler {
        return(x) { x }
        askElabA() { resume(e) }
    }
};

handle[hElab(eOne)] {
    let g = handle[hElab(eTwo)] {
        let f = fn(e) { elab[askElabA()] a!() };
        if k {
            f
        } else {
            f();
            fn() { () }
        }
    }
    g()
}
\end{lstlisting}

\TODO{Define the transformation.}

\section{Compiling Elaborations into Handlers}

Combining the ideas above, we obtain a surprisingly simple transformation. Each elaboration is transformed into a handler, which resumes with a function containing the original expression, where argument occurrences force the thunked values. Since elaborations are now handlers, we need to change the \el{elab} constructs to \el{handle} constructs accordingly. Finally, the arguments to operation calls are thunked and the function that is resumed is called, that is, there is an additional \el{()} at the end of the operation call.

\begin{figure}[H]
\begin{tabular}{rcl}
\begin{lstlisting}
elab[$e_1$] {$e_2$}
\end{lstlisting}
& $\implies$
& \begin{lstlisting}
handle[$e_1$] {$e_2$}
\end{lstlisting}
\\\\
\begin{lstlisting}
elaboration {
    $op_1!(x_{1,1}, ..., x_{k_1,1})$ { $e_1$ }
    ...
    $op_n!(x_{1,n}, ..., x_{k_n,n})$ { $e_n$ }
}
\end{lstlisting}
&$\implies$
&\begin{lstlisting}
handler {
    $op_1(x_{1,1}, ..., x_{k_1,1})$ {
        resume(fn() {$e_1$[$x_{i,1} \mapsto x_{i,1}()$]})
    }
    ...
    $op_n(x_{1,n}, ..., x_{k_n,n})$ {
        resume(fn() {$e_1$[$x_{i,n} \mapsto x_{i,n}()$]})
    }
}
\end{lstlisting}
\\\\
\begin{lstlisting}
$op_j$!($e_1,...,e_k$)
\end{lstlisting}
& $\implies$
& \begin{lstlisting}
$op_j$(fn() \{$e_1$\}, ..., fn() \{$e_k$\})()
\end{lstlisting}
\end{tabular}
\end{figure}

\section{An Alternative Design for Elaine}

The simplicity of the transformation makes it alluring and begs the question: are dedicated language features for higher-order effects necessary or is a simpler approach possible?

Since we can encode elaborations as handlers, we can write higher-order effects as the result of the transformation above directly. Below is an example of an exception effect written in this style.

\begin{lstlisting}[language=elaine,style=fancy]
effect Exc {
    catch(fn() <Throw> a, fn() a)
}

let hExc = handler {
    return(x) { x }
    catch(f, g) {
        resume(fn() {
            let res = handle[hThrow] f();
            match res {
                Just(x) => x,
                Nothing => g(),
            }
        })
    }
};

let main = handle[hCatch] catch(
    fn() { ... },
    fn() { ... },
)();
\end{lstlisting}

Now let us introduce a few (hypothetical) syntactic conveniences. First, like Koka, we let \el{\{...\}}| represent \el{fn()\{...\}}. Second, we allow the \el{return} case to be omitted and default to the identity function. Third, if an operation ends with \el{!}, the operation body is wrapped in \el{resume(\{...\})} and the operations is given an underscore prefix with the original name brought into scope defined as

\begin{lstlisting}[language=elaine,style=fancy]
let op! = fn($x_1$, ..., $x_n$){ _op!($x_1$, ..., $x_n$)() };
\end{lstlisting}

Our previous example then reduces to

\begin{lstlisting}[language=elaine,style=fancy]
effect Exc {
    catch!(fn() <Throw> a, fn() a)
}

let hExc = handler {
    catch!(f, g) {
        let res = handle[hThrow] f();
        match res {
            Just(x) => x,
            Nothing => g(),
        }
    }
};

let main = handle[hExc] catch!(
    { ... },
    { ... },
);
\end{lstlisting}

The result is almost as convenient as the version of Elaine presented in the previous chapters and does not have the split between elaborations and handlers, which possibly makes it easier to understand. The same design could be applied to other implementations of algebraic effects as well with relative ease.

To illustrate that point, below is modular \el{catch} effect in Koka. While the handler is certainly more verbose than handlers for algebraic effects in Koka, the implementation is also quite simple. Koka could implement a shorthand for creating the boilerplate and essentially the same functionality as Elaine.

\begin{lstlisting}[language={},style=fancy]
effect abort
  ctl abort(): a
  
effect exc
  fun catch_( f : () -> <abort|e> a, g : () -> e a ) : (() -> e a)
  
val hAbort = handler
  return(x)   Just(x)
  ctl abort() Nothing

val hExc = handler
  fun catch_(f, g)
    fn()
      match hAbort(f)
        Just(x) -> x
        Nothing -> g()

fun catch(f, g)
  catch_(f, g)()

fun main()
  with hExc
  println(catch({ if True then abort() else 5 }, { 0 }))
\end{lstlisting}

Because we can then ``emulate'' higher-order effects, the importance for explicit support for them is reduced. An advantage of this approach is that, because we do not have to elaborate into algebraic effects, we do not have to handle them. In Elaine the \el{main} function looks like this:

\begin{lstlisting}[language=elaine,style=fancy]
let main = handle[hAbort] elab catch!(if true { abort() } else { 5 }, { 0 });
\end{lstlisting}

\TODO{Talk about impredicativity and how that makes it so that it does not work.}

