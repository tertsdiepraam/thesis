\chapter{Higher-Order Effects}\label{chap:higher_order}

In the previous chapter, we explained the concept of algebraic effects. Any effect that satisfies the algebraicity property is algebraic. However, many \emph{higher-order effects} are not algebraic. An effect is higher-order if one of its operations takes effectful computations as parameters \autocite{bach_poulsen_hefty_2023}. As a result, it is not possible to give modular implementations for these operations using effect handlers, like we can do for algebraic operations. This chapter details the difficulties around higher-order effects and discusses hefty algebras, the theory that Elaine is based on.

\section{Computation Parameters}

Recall that an effect in the free monad encoding is a functor over some \hs{k} with some constructors. The type \hs{k} represents the continuation of the computation. Naturally, it is possible to write a constructor with multiple parameters of type \hs{k}. For example, we could make a \hs{Branch} functor which takes a boolean and two computations. Based on the boolean value, it selects the branch to evaluate. It is essentially an \hs{if-else} expression expressed as an effect.

\begin{lst}{Haskell}
data Branch k = Branch Bool k k

branch :: Branch < f => Bool -> Free f a -> Free f a -> Free f a
branch b ifTrue ifFalse :: Op $ inj $ Branch b ifTrue ifFalse
\end{lst}
%
The important observation with this effect is that both \hs{ifTrue} and \hs{ifFalse} behave like continuations. To examine why, consider the following computation.

\begin{lst}{Haskell}
branch b (pure 0) (pure 1) >>= \x -> pure (x + 1)
\end{lst}
%
Like previously established, the \hs{>>=} operator defined by \hs{Free} distributes over the computation parameters. This yields the following expression.

\begin{lst}{Haskell}
branch b
  (pure 0 >>= \x -> pure (x + 1))
  (pure 1 >>= \x -> pure (x + 1))
-- which reduces to
branch b (pure 1) (pure 2)
\end{lst}
%
This computation has the same intended semantics as the original. The distribution of \hs{>>=} therefore does not change the semantics and hence the effect is algebraic. Therefore, there would be no problem encoding this effect in Haskell using the encoding in the previous chapter and, by extension, in Koka.

This is what we mean by saying that the parameters are computation-like: the continuation can be appended to the parameters without changing the semantics of the effect.

\section{Breaking Algebraicity}

For other effects, however, the intended semantics are not such that the computation parameters are continuation-like.

One such effect is the \hs{Reader} effect. Traditionally, the \hs{Reader} monad has two operations: \hs{local} and \hs{ask}. The latter functions much like the \hs{get} operation from the state effect and is algebraic. However, the \hs{local} operation is more complex. It takes two parameters, a function \hs{f} and a computation \hs{c}. The intended semantics are then that whenever \hs{ask} is used within \hs{c}, the function \hs{f} is applied to the returned value. To see why the \hs{local} operation breaks algebraicity, consider the following computation.

\begin{lst}{Haskell}
local (* 2) ask >>= \x -> ask >>= \y -> pure x + y
\end{lst}
%
Only the first \hs{ask} operation is inside the \hs{local} operation and should therefore be doubled. If the \hs{Reader} effect was algebraic, we should be able to distribute the \hs{>>=} operator again without changing the semantics of the program. However, doing so yields the following computation.

\begin{lst}{Haskell}
local (* 2) (ask >>= \x -> ask >>= \y -> pure x + y)
\end{lst}
%
Now, both \hs{ask} operations are inside the \hs{local} operation, so both values will be doubled. For example, if we had installed a handler that makes \hs{ask} return 1, the first computation would return $2 + 1 = 3$ and the second $2 + 2 = 4$. Therefore, we have shown with a counterexample that the \hs{Reader} effect cannot be algebraic.

A similar argument holds for the \hs{Except} effect, which also has two operations: \hs{catch} and \hs{throw}. In the simplest form, \hs{throw} resembles the \hs{abort} effect, but it takes a parameter that represents an error message. The \hs{catch} operation evaluates its first parameter and jump to the second if it fails, much like the try-catch constructs of languages with effects. Again, we examine a simple example program to show that \hs{Except} violates algebraicity.

\begin{lst}{Haskell}
catch (pure False)  (pure True) >>= throw -- -> throws False
-- then distributing >>= yields
catch
  (pure False >>= throw)
  (pure True >>= throw)
-- which simplifies to
catch (throw False) (throw True)          -- -> throws True
\end{lst}
%
Before distributing the \hs{>>=} operator the computation should throw \hs{False}, but after it should throw \hs{True}. So, again, the semantics have changed by distributing the \hs{>>=} and therefore \hs{Except} is not algebraic.

\section{The Modularity Problem}

Taking a step back from effects, defining a function for exception catching is possible. Recall that the \hs{throw} operation is algebraic, therefore, a handler for it can be defined. If we assume some handler for it called \hs{handleThrow} returns an \hs{Either} where \hs{Left} is the value from \hs{throw} and \hs{Right} is the value from a completed computation, we can define \hs{catch} in terms of that function.

\begin{lst}{Haskell}
catch c1 c2 =
  case handleThrow c1 of
    Left e -> c2
    Right a -> return a
\end{lst}
%
The distinction between effects which are and which are not algebraic has been described as the difference between \emph{effect constructors} and \emph{effect deconstructors} \autocite{plotkin_algebraic_2003}. The \el{local} and \el{catch} operations have to act on effectful computations and change the meaning of the effects in that computation. So, they have to deconstruct the effects in their computations using handlers. An imperfect heuristic for whether a function can be an algebraic effect is to check whether the implementation requires a handler. If it uses a handler, it probably cannot be an algebraic effect.

An algebraic effect can have a modular implementation: a computation can be reused in different contexts by using different handlers. For these higher-order effects such as \hs{catch} and \hs{local}, this is not possible. This is known as the \emph{modularity problem} with higher-order effects \autocite{wu_effect_2014}. This is the motivation behind the research on higher-order effects, including this thesis. It is also the problem that the theory of hefty algebras aims to solve.

\section{Hefty Algebras}\label{sec:hefty_algebras}

Several solutions to the modularity problem have been proposed \autocite{wu_effect_2014,oh_latent_2021}. Most recently, \textcite{bach_poulsen_hefty_2023} introduced a solution called hefty algebras. The idea behind hefty algebras is that there is an additional layer of modularity, specifically for higher-order effects.

For a full treatment of hefty algebras, we refer to \textcite{bach_poulsen_hefty_2023}. In addition, the encoding of hefty algebras is explained in more detail by \textcite{bach_poulsen_algebras_2023}.

At the core of hefty algebras are hefty trees. A hefty tree is a generalization of the free monad to higher-order functors, which will write \hs{HFunctor}. In the listing below, we also repeat the definition of a functor from the previous chapter for comparison.

\begin{lst}{Haskell}
-- a regular functor
class Functor f where
  fmap :: (a -> b) -> f a -> f b

-- a higher-order functor
class (forall f. Functor (h f)) => HFunctor h where
  hmap :: (f a -> g a) -> (h f a -> h g a)
\end{lst}
%
The definition of a hefty tree, with the free monad for reference, then becomes:

\begin{lst}{Haskell}
-- free monad
data Free f a
  = Pure a
  | Op (f (Free f a))

-- hefty tree
data Hefty h a
  = Return a
  | Do (h (Hefty h) (Hefty h a))
\end{lst}
%
A hefty tree and a free monad are very similar. Like for the free monad, we can define \hs{return} and \hs{>>=} for \hs{Hefty h}, so that it can be used as a monad. Additionally, we define \hs{injH}, \hs{:<:}, and \hs{:+:} as analogues for \hs{inj}, \hs{<}, and \hs{+} from the previous chapter, respectively. We refer to \textcite{bach_poulsen_algebras_2023} for the definition of these operators. Furthermore, any functor can be lifted to a higher-order functor with a \hs{Lift} data type.

\begin{lst}{Haskell}
data Lift f (m :: * -> *) k = Lift (f k)
  deriving Functor

instance Functor f => HFunctor (Lift f) where
  hmap _ (Lift x) = Lift x
\end{lst}
%
In algebraic effects, the evaluation of a computation can be thought of as a transformation of the free monad to the final result: 
\[
    \mcode{Free f a} \quad\xrightarrow{handle}\quad \mcode{b}.
\]
Using hefty algebras, the evaluation instead starts with a \emph{hefty tree}, which is \emph{elaborated} into the free monad. The full evaluation of a computation using hefty algebras then becomes:
\[
    \mcode{Hefty h a} \quad\xrightarrow{elaborate}\quad \mcode{Free f a} \quad\xrightarrow{handle}\quad \mcode{b}.
\]
This elaboration is a transformation from a hefty tree into the free monad, defined as an algebra over hefty trees. The algebras are then used in \hs{hfold}; a fold over hefty trees.

\begin{lst}{Haskell}
hfold :: HFunctor h
      => (forall a. a -> g a)
      -> (forall a. h g (g a) -> g a)
      -> Hefty h a 
      -> g a
hfold gen _   (Return x) = gen x
hfold gen alg (Do x)     =
  alg (fmap (hfold gen alg) (hmap (hfold gen alg) x))

elab :: HFunctor h
     => (forall a. h (Free f) (Free f a) -> Free f a)
     -> Hefty h a
     -> Free f a
elab elabs = hfold Pure elabs
\end{lst}
%
For any algebraic -- and thus lifted -- effect, this elaboration \hs{eLift} is trivially defined by unwrapping the \hs{Lift} constructor. Applying \hs{eLift} to \hs{elab} then gives a function which elaborates \hs{Hefty (Lift f) a} to \hs{Free f a} for any functor \hs{f}. 

\begin{lst}{Haskell}
eLift :: g < f => Lift g (Free f) (Free f a) -> Free f a
eLift (Lift x) = Op (inj x)
\end{lst}
%
To elaborate multiple effects, elaborations can be composed using the \hs{^} operator as defined by \textcite{bach_poulsen_hefty_2023}. The composed elaborations are then applied all at once, because an elaboration of a \hs{Hefty h a} returns a \hs{Free f a}, which cannot contain any higher-order effects.

Using elaborations, we can modularly define higher-order operations, such as \hs{catch} and \hs{local} operations. For example, the \hs{Except} effect can be elaborated to a computation using only the algebraic \hs{ThrowA} effect. This elaboration is given in \cref{fig:except}.\footnote{This code is simplified and does not compile in this form. In reality the result of the handler needs to be wrapped in a function \hs{hup}, which reorders the effects in the effect row to match the context. The same goes for the example for the reader effect below. This \hs{hup} requires some additional machinery as explained by \textcite{bach_poulsen_encoding_2023}.}

For the \hs{Reader} effect, the \el{local} operation needs to be elaborated, but the \el{ask} operation can be handled. So, we define an algebraic effect \el{AskA} with only the \el{ask} operation. This effect is handled by \hs{handleAskA}. The implementation of both the handler and elaboration are listed in \cref{fig:reader}.

The \hs{catch} and \hs{local} operations in these examples are modular, because the elaborations can be swapped out for different elaborations. Additionally, we can write computations using both effects. For example, the elaboration of a computation with both a catch and reader effect looks as follows:

\begin{lst}{Haskell}
main = handleThrow $ handleAsk 0 $
  elab (eCatch ^ eReader ^ eLift) c
\end{lst}
%
Similar to how Koka is based on the theory of algebraic effects, Elaine is based on hefty algebras. Therefore, Elaine supports elaborations for higher-order effects. While the intended semantics for expressions in Elaine are hefty trees, the semantics of Elaine's \el{elab} construct are currently unclear, as discussed in \cref{ssec:denosem}.

\begin{figure}[p]
\begin{lst}{Haskell}
data ThrowA e k = ThrowA e
  deriving Functor

handleThrowA :: Functor f
             => Free (Abort + f) a -> Free f (Maybe a)
handleThrowA c = handle
    (\a _ -> Pure $ Right a)
    (\(Throw e) () -> Pure $ Left e) 
    c ()

data Except e f k
  = Throw e
  | Catch (f a) (f a) (a -> k)

deriving instance Functor (Except e f)

instance HFunctor (Except e) where
  hmap _ (Throw x) = Throw x
  hmap f (Catch m1 m2 k) = Catch (f m1) (f m2) k

throw :: Except e :<: h => e -> Hefty h a
throw x = Op (injH (Throw x))

catch :: Except :<: h
      => Hefty h a -> Hefty h a -> Hefty h a
catch m1 m2 = Do $ inj $ Catch m1 m2 Return

eExcept :: ThrowA e < f
        => Except e (Free f) (Free f a)
        -> Free f a
eExcept (Throw e) = Do (inj (ThrowA e))
eExcept (Catch m1 m2 k) =
  handleThrowA c1 >>= \v ->
    case v of
      Left e -> c2 >>= k
      Right x  -> k x

comp :: Hefty (Except e :+: Lift (ThrowA e)) Int
comp = catch (throw) (return 4)

result = handleAbort $ elab (eExcept ^ eLift) comp
\end{lst}
\caption{Definition, elaboration and use of the \hs{Except} effect.}
\label{fig:except}
\end{figure}

\begin{figure}[p]
\begin{lst}{Haskell}
data AskA r k = AskA (r -> k)
  deriving Functor

handleAskA :: Functor f
           => r -> Free (AskA + f) a -> Free f a 
handleAskA v m =
  handle
    (\a () -> Pure a)
    (\(Ask k) -> k v)
    m ()

data Reader r m k =
  = Ask (r -> k)
  | forall a. Local (r -> r) (m a) (a -> k)

deriving instance Functor (Reader r m)

instance HFunctor (Reader r) where
  hmap _ (Ask k) = (Ask k)
  hmap f (Local f m k) = Local g (f m) k

ask :: Reader r :<: h
    => Hefty h r
ask = Do $ injH $ Ask Pure

local :: Reader r :<: h
      => (r -> r) -> Hefty h a -> Hefty h a
local f m = Do $ injH $ Local f

eReader :: AskA r < f
        => Reader r (Free f) (Free f a)
        -> Free f a
eReader (Ask k) = Op $ inj $ AskA k
eReader (Local f m k) = 
  askA >>= \r ->
    handleAskA (f r) m >>=
      k

comp :: Hefty (Reader Int :+: Lift Abort) Int
comp = ask >>= \x ->
        local (* 2) (ask >>= \y -> return (x + y))
                          
result = handleAbort $ elab (eReader ^ eLift) comp
\end{lst}
\caption{Definition, elaboration and use of the \hs{Reader} effect.}
\label{fig:reader}
\end{figure}
